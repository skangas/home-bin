#!/usr/bin/perl

use 5.010_000;
use warnings;
use strict;

=head1 NAME

webpass - generate SHA-1 passwords using file as salt

=cut

use Data::Dumper;
use Digest::SHA qw(sha1_base64);
use File::Slurp qw(slurp);
use Getopt::Long;

my %options = (
    'debug' => 0,
    'old'   => 0,
    'print' => 0,
    );

GetOptions(
    'debug' => \$options{debug},
    'old'   => \$options{old},
    'print' => \$options{print},
    ) or die "Unable to get command line options";

my $site = $ARGV[0];
my $passfile = $ARGV[1];

$site //= "";
if ($options{old}) {
    dprint("Using old");
    $passfile //= "$ENV{HOME}/bin/.webpass-old";
} else {
    dprint("Using new");
    $passfile //= "$ENV{HOME}/bin/.webpass";
}

dprint("Site:     '$site'");
dprint("Passfile: '$passfile'");

my $pass = slurp($passfile);
chomp($pass);
# dprint("Password: '$pass'");

my $password = sha1_base64($pass . ":" . $site);
if ($options{old}) {
    $password = substr($password, 0, 8) . '1a';
} else {
    $password = substr($password, 0, 13) . '@1a';
}

if ($options{print}) {
    say "Password: $password";
}

Clipboard->copy($password);

sub dprint {
    if ($options{debug}) {
        say @_;
    }

}

=head1 SEE ALSO

http://www.angel.net/~nic/passwd.sha1.1a.html

=head1 AUTHOR

Stefan Kangas C<< <skangas at skangas.se> >>

=head1 COPYRIGHT

Copyright 2010-2016 Stefan Kangas, all rights reserved.

=head1 LICENSE

This program is free software; you can redistribute it and/or modify it
under the same terms as perl itself.

=cut

#### INLINED CLIPBOARD MODULE #####
# This code is from Cliboard v0.13 by Ryan King (found on CPAN)
BEGIN {
    package Clipboard;
    sub copy {
        my $self = shift;
        my ($input) = @_;
        $self->copy_to_selection($self->favorite_selection, $input);
    }
    sub copy_to_selection {
        my $self = shift;
        my ($selection, $input) = @_;
        my $cmd = '|xclip -i -selection '. $selection;
        my $r = open my $exe, $cmd or die "Couldn't run `$cmd`: $!\n";
        print $exe $input;
        close $exe or die "Error closing `$cmd`: $!";
    }
    sub paste {
        my $self = shift;
        for ($self->all_selections) {
            my $data = $self->paste_from_selection($_); 
            return $data if length $data;
        }
        undef
    }
    sub paste_from_selection {
        my $self = shift;
        my ($selection) = @_;
        my $cmd = "xclip -o -selection $selection|";
        open my $exe, $cmd or die "Couldn't run `$cmd`: $!\n";
        my $result = join '', <$exe>;
        close $exe or die "Error closing `$cmd`: $!";
        return $result;
    }
    # This ordering isn't officially verified, but so far seems to work the best:
    sub all_selections { qw(primary buffer clipboard secondary) }
    sub favorite_selection { my $self = shift; ($self->all_selections)[0] }
    {
        open my $just_checking, 'xclip -o|' or warn <<'EPIGRAPH';

Can't find the 'xclip' script.  Clipboard.pm's X support depends on it.

Here's the project homepage: http://sourceforge.net/projects/xclip/

EPIGRAPH
    }
}
