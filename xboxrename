#!/usr/bin/perl

# xboxrename -- rename files into something that is accepted by the Xbox file
# system (XFAT)
#
##
#
# Copyright © 2005-2010 Stefan Kangas <skangas@skangas.se>
#
##
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

use 5.010;
use warnings;
use strict;
use utf8;
use encoding ':locale';

use Getopt::Long;
use Path::Class;

my $execute;
GetOptions( 'execute'    => \$execute );

for my $name (glob "*") {
    # Since glob doesn't use utf8 by default we need to explicitly upgrade here
    # in addition to use encoding above
    utf8::upgrade($name);
    my $newname = mk_xbox_name($name);

    if ( $name ne $newname && ! -e $newname ) {
        printf("%s -> %s\n", $name, $newname);
        if ($execute) {
            rename $name, $newname;
        }
    }
}

sub mk_xbox_name {
    my ($name) = @_;

    ### NOTE: Order of the regexps matter

    ### 1
    
    ### Convert swedish characters
    $name =~ s/[åä]/a/g;
    $name =~ s/ö/o/g;

    ### Remove some shit
    $name =~ s/\[.+?\]       ### [brackets] including their content
               |[^[:ascii:]] ### everything outside ASCII
               |[²!'`´,]     ### remove some other weird characters
               |\n|\r|\t     ### newline or tab shouldn't be in filename
              //msxg;

    ### 2
    
    ### Strip the extension 
    my $ext;
    if (! -d $name) {
	$ext = (split /[.]/, $name)[-1];
	$name =~ s/\.$ext$//;
	$ext = lc $ext;
	$ext =~ s/mpeg/mpg/;
    }

    ### 3
    
    ### Remove duplicate word delimiters
    my $delimiter = '[\s_.]';
    $name =~ s/($delimiter)+/$1/g;
    ### Remove leading and trailing word delimiters 
    $name =~ s/\A$delimiter+
           |$delimiter+\z
              //msxg;
    ### remove dash
    $name =~ s/$delimiter-$delimiter/-/g;

    if (-d $name) {
	return sprintf('%.42s', $name);
    } else {
        my $extlen = length $ext;
        my $ext_format  = '%.' . $extlen        . 's';
        my $name_format = '%.' . (41 - $extlen) . 's';
        my $format      = "$name_format.$ext_format";
	return sprintf $format, $name, $ext;
    }
}
